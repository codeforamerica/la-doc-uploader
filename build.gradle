plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'com.adarshr.test-logger' version '3.2.0'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def props = new Properties()
if (file(".env").exists()) {
    file(".env").withInputStream { props.load(it) }
}

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    mavenCentral()
}

def profile = props.getProperty('SPRING_PROFILES_ACTIVE')
def formFlowLibraryVersion = '0.0.2-SNAPSHOT'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation("com.mixpanel:mixpanel-java:1.5.1")

    implementation 'org.flywaydb:flyway-core'

    if (profile == 'dev') {
        implementation fileTree(dir: "$rootDir/../form-flow/build/libs", include: '*.jar')
        println "ðŸ“¦ Using local library"
    } else {
        implementation "org.codeforamerica.platform:form-flow:${formFlowLibraryVersion}"
        println "ðŸ“šUsing form flow library ${formFlowLibraryVersion}"
    }

    implementation 'com.google.crypto.tink:tink:1.9.0'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.8.1'
    testImplementation 'io.percy:percy-java-selenium:1.3.2'
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.3.2'

    runtimeOnly 'org.postgresql:postgresql'
}

springBoot {
    buildInfo()
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    environment("ENCRYPTION_KEYSET", "{\"primaryKeyId\":2135185311,\"key\":[{\"keyData\":{\"typeUrl\":\"type.googleapis.com/google.crypto.tink.AesGcmKey\",\"value\":\"GiCRKaXiJ/zlDHAZfRQf1rCIbIY4fFmLqLWYIPLNXpOx4A==\",\"keyMaterialType\":\"SYMMETRIC\"},\"status\":\"ENABLED\",\"keyId\":2135185311,\"outputPrefixType\":\"TINK\"}]}")
    environment("DEFAULT_LOCALE", "en")
    environment("MIXPANEL_API_KEY", "this-is-a-dummy-key-for-la-tests")
}

jar {
  enabled false
}
